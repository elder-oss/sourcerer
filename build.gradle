buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.11.0"
    }
}

plugins {
    id "org.sonarqube" version "2.6.2"
}

apply plugin: 'io.codearte.nexus-staging'

def getVersionName = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

nexusStaging {
    packageGroup = "org.elder.sourcerer"
    username = ossrhUsername
    password = ossrhPassword
}

if ("$enable_signing" == 'true') {
    project.ext.set("use_signing", true)
    project.ext.set("signing.keyId", sourcerer_signing_keyId)
    project.ext.set("signing.password", sourcerer_signing_password)
    project.ext.set("signing.secretKeyRingFile", sourcerer_signing_secretKeyRingFile)
}

sonarqube {
    properties {
        property "sonar.projectVersion", getVersionName()
        property "sonar.jacoco.reportPath", "build/coverage/jacoco.exec"
        property "sonar.analysis.mode", "preview"
        property "sonar.host.url", sourcerer_sonar_host_url
        property "sonar.login", sourcerer_sonar_login
        property "sonar.github.oauth", sourcerer_sonar_github_oauth
        property "sonar.github.repository", "elder-oss/sourcerer"
    }
}

subprojects {
    group = 'org.elder.sourcerer'
    version = getVersionName()

    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'checkstyle'
    apply plugin: 'jacoco'
    apply plugin: 'signing'

    sourceCompatibility = 1.10
    targetCompatibility = 1.10

    checkstyle {
        configFile = new File(rootDir, "infra/codehealth/checkstyle.xml")
        toolVersion = "8.8"
        ignoreFailures = false
    }

    tasks.withType(Checkstyle) {
        exclude '**/module-info.java'
    }

    jacoco {
        reportsDir = file("build/coverage/jacoco.exec")
    }

    repositories {
        mavenCentral()
    }

    configurations.all {
        // Conflicts with javax.annotations
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
    }

    dependencies {
        implementation "org.slf4j:slf4j-api:$slf4jVersion"
        testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
        testImplementation "org.junit.vintage:junit-vintage-engine:$junitVersion"
        testImplementation 'org.assertj:assertj-core:3.9.1'
        testImplementation "org.mockito:mockito-core:$mockitoVersion"
        testImplementation "org.powermock:powermock-core:$powermockVersion"
        testImplementation "org.powermock:powermock-module-junit4:$powermockVersion"
        testImplementation "org.powermock:powermock-api-mockito2:$powermockVersion"
    }

    test {
        useJUnitPlatform()
        reports {
            junitXml.enabled = true
            html.enabled = true
        }
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }

    task downloadDependencies(type: Copy) {
        from configurations.testRuntime
        into 'build/tmp/dependencieshack/'
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java

                artifact sourcesJar {
                    classifier "sources"
                }

                artifact javadocJar {
                    classifier "javadoc"
                }
            }
        }

        repositories {
            maven {
                credentials {
                    username property("sourcerer_nexus_username")
                    password property("sourcerer_nexus_password")
                }
                url property("sourcerer_nexus_releaseUrl")
            }
        }
    }

    signing {
        required { "$enable_signing" == 'true' }
        sign configurations.archives
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project {
                    name 'Elder Sourcerer'
                    packaging 'jar'
                    description 'An opinionated framework for implementing an CQRS architecture using event sourcing'
                    url 'https://github.com/elder-oss/sourcerer'

                    scm {
                        connection 'scm:git:https://github.com/elder-oss/sourcerer.git'
                        developerConnection 'scm:svn:https://github.com/elder-oss/sourcerer.git'
                        url 'https://github.com/elder-oss/sourcerer'
                    }

                    licenses {
                        license {
                            name 'The MIT License (MIT)'
                            url 'https://opensource.org/licenses/MIT'
                        }
                    }

                    developers {
                        developer {
                            id 'elder-developers'
                            name 'Developers At Elder'
                            email 'tech@elder.org'
                        }
                    }
                }
            }
        }
    }

    afterEvaluate {
        compileJava {
            inputs.property("moduleName", moduleName)
            doFirst {
                options.compilerArgs = [
                        '--module-path', classpath.asPath,
                ]
                classpath = files()
            }
        }

        compileTestJava {
            inputs.property("moduleName", moduleName)
            doFirst {
                options.compilerArgs = [
                        '--module-path', classpath.asPath,
                        '--add-modules', 'junit',
                        '--add-reads', "$moduleName=junit",
                        '--patch-module', "$moduleName=" + files(sourceSets.test.java.srcDirs).asPath,
                ]
                classpath = files()
            }
        }

        test {
            inputs.property("moduleName", moduleName)
            doFirst {
                jvmArgs = [
                        '--module-path', classpath.asPath,
                        '--add-modules', 'ALL-MODULE-PATH',
                        '--add-reads', "$moduleName=junit",
                        '--patch-module', "$moduleName=" + files(sourceSets.test.java.outputDir).asPath,
                ]
                classpath = files()
            }
        }

        javadoc {
            inputs.property("moduleName", moduleName)
            doFirst {
                options.addStringOption('-module-path', classpath.asPath)
            }
        }
    }
}
